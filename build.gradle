plugins {
    id 'java'
    id 'groovy'
    id 'maven-publish'
}

group 'tk.amplifiable.mcgradle'
version '1.1.0'

repositories {
    mavenCentral()
    maven {
        url 'https://files.minecraftforge.net/maven'
    }
}

dependencies {
    implementation gradleApi()
    implementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '5.5.1.201910021850-r'
    compile group: 'com.google.guava', name: 'guava', version: '28.1-jre'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    compile group: 'org.ow2.asm', name: 'asm-all', version: '5.2'
    compile 'net.sf.opencsv:opencsv:2.3'
    compile 'de.oceanlabs.mcp:RetroGuard:3.6.6'
    compile 'de.oceanlabs.mcp:mcinjector:3.4-SNAPSHOT'
    compile 'net.minecraftforge.srg2source:Srg2Source:3.3-SNAPSHOT'
    compile 'net.md-5:SpecialSource:1.7.4'
    compile 'com.cloudbees:diff4j:1.1'
    compile 'net.minecraftforge:fernflower:2.0-SNAPSHOT'
    compile 'com.github.abrarsyed.jastyle:jAstyle:1.3'
    compile group: 'com.nothome', name: 'javaxdelta', version: '2.0.1'
    compile 'com.github.jponge:lzma-java:1.3'
    compile 'io.github.java-diff-utils:java-diff-utils:4.4'
}

// Use Groovy
sourceSets {
    main {
        java { srcDirs = [] }
        groovy { srcDirs = ["src/main/java", "src/main/groovy"] }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    repositories {
        maven {
            url = "$buildDir/repository"
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'MCGradle'
                description = 'Gradle plugin for Minecraft modding'
                url = 'https://github.com/AmpTheDev/MCGradle'
                licenses {
                    license {
                        name = 'GNU LGPL'
                        url = 'https://raw.githubusercontent.com/AmpTheDev/MCGradle/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'ampthedev'
                        name = 'AmpTheDev'
                        email = 'ampthedev@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/AmpTheDev/MCGradle.git'
                    developerConnection = 'scm:git:ssh://github.com/AmpTheDev/MCGradle.git'
                    url = 'https://github.com/AmpTheDev/MCGradle'
                }
            }
        }
    }
}